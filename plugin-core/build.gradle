apply plugin: 'groovy'
apply plugin: 'idea'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
        classpath "gradle.plugin.org.mikeneck.gradle.plugin:rule-base-model-generation-plugin:0.3.0"
    }
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: 'org.mikeneck.rule-based-model-generation'

apply from: rootProject.file('jdk7-settings.gradle')

group = 'org.mikeneck.gradle.plugin'
version = '0.1'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "fish.payara.extras:payara-micro:${payaraVersion}"
    compile gradleApi()
    testCompile gradleTestKit()
}

model {
    metaModel {
        srcDir = 'src/main/groovy'
        packageName = 'org.mikeneck.gradle.plugin.payara.model'
        classes.create {
            name = 'PayaraSetting'
            type = INTERFACE
            fields.create {
                name = 'httpPort'
                type = INTEGER
            }
            fields.create {
                name = 'stopPort'
                type = INTEGER
            }
            fields.create {
                name = 'stopCommand'
                type = STRING
            }
            fields.create {
                name = 'daemon'
                type = BOOLEAN
            }
        }
    }
}

pluginBundle {
    website = 'https://github.com/mike-neck/gradle-payara-unofficial-plugin'
    vcsUrl = 'https://github.com/mike-neck/gradle-payara-unofficial-plugin'
    description = """\
           |Payara plugin.
           |
           |With this plugin you can run JavaEE application on payara-micro.
           |""".stripMargin()
    plugins {
        payaraUnofficialPlugin {
            id = 'org.mikeneck.payara-plugin'
            displayName = 'Payara (Unofficial)Plugin'
            tags = ['payara', 'war', 'JavaEE']
        }
    }
}
